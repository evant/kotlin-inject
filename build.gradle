plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version" apply false
    id 'com.google.devtools.ksp' version "$ksp_version" apply false
    id "io.gitlab.arturbosch.detekt" version "1.15.0"
}

repositories {
    jcenter()
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }
    afterEvaluate { project ->
        project.tasks.findByName("check")?.dependsOn rootProject.tasks["detekt"]
    }
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
    parallel = true
    failFast = false
    buildUponDefaultConfig = true
    source = files(projectDir)
    config.from = project.file(".static/detekt-config.yml")
    include("**/*.kt")
    exclude("**/build/**")
    autoCorrect = findProperty('fix') == 'true'
    jvmTarget = "1.8"
    reports {
        html {
            enabled = true
            destination = project.file("build/reports/detekt.html")
        }
        xml.enabled = false
    }
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.14.2"
}