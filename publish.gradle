apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'de.marcphilipp.nexus-publish'

group = 'me.tatarka.inject'
version = '0.3.4-SNAPSHOT'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

nexusPublishing {
    repositories {
        sonatype()
    }
    clientTimeout.set(Duration.ofMinutes(5))
    connectTimeout.set(Duration.ofMinutes(5))
}

def customArtifactId = project.ext.find("artifactId")

publishing {
    publications {
        lib(MavenPublication) {
            if (customArtifactId != null) {
                artifactId customArtifactId
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'kotlin-inject'
                description = 'A compile-time dependency injection library for kotlin'
                url = 'https://github.com/evant/kotlin-inject'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'evant'
                        name = 'Evan Tatarka'
                    }
                }
                scm {
                    connection = 'https://github.com/evant/kotlin-inject.git'
                    developerConnection = 'https://github.com/evant/kotlin-inject.git'
                    url = 'https://github.com/evant/kotlin-inject'
                }
            }
        }
    }
}

signing {
    required { project.findProperty('signing.keyId') != null }
    sign publishing.publications.lib
}